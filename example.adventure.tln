###GLOBAL###
understand "look" as describing. 
understand "north", "south", "east", "west", "up", "down" as directions. 
understand "go" as moving. 
understand "take" as taking. 
understand "give" as giving.
understand "inv", "i", "inventory" as inventory. 
understand "drop" as dropping. 
understand "use" as using.
understand "open" as opening.
understand "close" as closing.
understand "combine" as combining.
understand "locked" as stateful.
understand "hold", "equip" as holding.
understand "Q" as options.

when the player starts:
    say "The player has started!";
and then stop.

when the player fails:
    say "Sorry, you failed to complete the game!";
and then stop.

when the player wins:
    say "You win!";
and then stop.

when the player is set:
    say "You selected a new player!";
and then stop.

when the game starts:
    say "Welcome to an example Talon adventure!";
    say "";
    show PlayerMenu;
and then stop.

when the game ends:
    say "The game has ended!";
and then stop.

when the game completes:
    say "You completed the game!";
and then stop.

when the player presses "Q":
    show MainMenu;
and then stop.

a PlayerMenu is a kind of menu.
it is described as "What kind of player do you want to use?".
it contains options WarriorOption, MageOption, ThiefOption.
when option WarriorOption is selected:
    say "";
    say "Your character is a warrior, very strong and tough but weak to magic.";
    set the player to Warrior;
    hide this;
and then stop.
when option MageOption is selected:
    say "";
    say "Your character is a mage, very good with magic but not very strong or tough.";
    set the player to Mage;
    hide this;
and then stop.
when option ThiefOption is selected:
    say "";
    say "Your character is a thief, knows a little magic and has some strength";
    set the player to Thief;
    hide this;
and then stop.

a WarriorOption is a kind of option.
it is described as "1) Warrior".

a MageOption is a kind of option.
it is described as "2) Mage".

a ThiefOption is a kind of option.
it is described as "3) Thief".

a MainMenu is a kind of menu.
it is described as "Main Menu".
it contains options ContinueOption, QuitOption.
when option QuitOption is selected:
    say "";
    say "I'm sorry to see you go, I hope you had fun!";
    hide this;
    quit;
and then stop.
when option ContinueOption is selected:
    say "";
    say "On with the adventure!";
    hide this;
and then stop.

a ContinueOption is a kind of option.
it is described as "1) Continue".

a QuitOption is a kind of option.
it is described as "2) Quit".

a Character is a kind of player.
it contains 10 Coin.
it has a value called currentHitpoints that is 20.
it has a value called maxHitpoints that is 20.
it has a value called score that is 0.
it has a value called leftHand that is empty and can hold a Weapon.
it has a value called rightHand that is empty and can hold a Weapon.
it has a value called body that is empty and can hold Armor.
when it holds a Weapon:
    show menu "Which hand should hold this?" with options:
        "1) Left Hand" will set the player's leftHand to Weapon;
        "2) Right Hand" will set the player's rightHand to Weapon;
        "3) Cancel" will cancel;
    and then hide;
    say "You hold the weapon!";
and then stop.
when it holds an Armor:
    set body to Armor;
    say "You put the armor on!";
and then stop.
when its body is set to LeatherArmor:
    say "The leather armor seems to fit you nicely!";
and then stop.
when its currentHitpoints is set to less than 1:
    say "Oh no, you took too much damage and died!";
    the player fails;
    the game ends;
and then stop.

a Warrior is a kind of Character.
it has a value called currentHitpoints that is 30.
it has a value called maxHitpoints that is 30.

a Mage is a kind of Character.
it has a value called currentHitpoints that is 15.
it has a value called maxHitpoints that is 15.

a Thief is a kind of Character.

###PLACES###
a WindingRoad is a kind of place.
it is where the player starts.
it contains 1 Sword, 1 Armor.
it is described as "You're on a winding dirt road, each side full of grass and weeds. The road to the north climbs higher and higher.".
it can reach the ScenicCliff by going "north".

a ScenicCliff is a kind of place.
it is described as "The road curves along to the east, running next to the edge of a cliff on the west. The road winds further to the south as well.".
it can reach the CliffBottom by going "west".
it can reach the WindingRoad by going "south".
it can reach the SecludedTemple by going "east".
when the player goes "west":
    say "Whoops, you got too close to the edge and fell off the cliff! You took 10 damage from the fall.";
    say "";
    subtract 10 from the player's currentHitpoints;
    say "You have {{the player's currentHitpoints}} HP left!";
    say "";
and then stop.

a CliffBottom is a kind of place.
it is described as "The bottom of the cliff is a small rocky beach, small waves crashing against it. To the north there appears to be a staircase cut into the rock.".
it contains 1 WaterloggedCorpse, 1 RelicPouch, 1 LeatherArmor.
it can reach the SecludedTempleStaircaseBottom by going "north".

a SecludedTemple is a kind of place.
it is described as "You arrive at a small temple nearly hidden by some trees. A path runs around the temple to the north, whereas to the east looks to be a grassy hill. To the west you can see that the path curves away.".
it contains 1 TempleDonationBox, 1 TempleDonationBoxPaper, 1 Monk.
it can reach the ScenicCliff by going "west".
it can reach the SecludedTempleGarden by going "north".
it can reach the GrassyHill by going "east".

a SecludedTempleGarden is a kind of place.
it is described as "You are behind the temple in a small garden area with various flowers and plants blooming around the edges. A path runs around the temple to the south, and there seems to be an entrance to a staircase going down over a cliff to the west.".
it contains 1 Monk.
it can reach the SecludedTemple by going "south".
it can reach the SecludedTempleStaircaseTop by going "west".
when the player goes "west":
    if it contains 1 Monk then
        say "The monk shakes his head sternly at you, warning you away from going any further. It looks like you can't go down the stairs at the moment.";
        abort event;
    and then continue;
and then stop.

a SecludedTempleStaircaseBottom is a kind of place.
it is described as "You are at the bottom of a very tall staircase heading upwards cut into the side of the cliff. To the south there seems to be a small beach nestled between the water and the cliff.".
it can reach the SecludedTempleStaircaseTop by going "up".
it can reach the CliffBottom by going "south".

a SecludedTempleStaircaseTop is a kind of place.
it is described as "You reach the top of a dizzyingly long staircase downwards that's cut into the side of the cliff. To the east there are some gardens and what looks like the back of a building. The wind blows and makes you wobble slightly.".
it can reach the SecludedTempleStaircaseBottom by going "down".
it can reach the SecludedTempleGarden by going "east".

a GrassyHill is a kind of place.
it can reach the SecludedTemple by going "west".
it can reach the ForestEntrance by going "east".

a ForestEntrance is a kind of place.
it can reach the GrassyHill by going "west".
it can reach the ForestEndlessPathway1 by going "south".

a ForestEndlessPathway1 is a kind of place.
it can reach the ForestEndlessPathway2 by going "north".
it can reach the ForestEndlessPathway3 by going "east".

a ForestEndlessPathway2 is a kind of place.
it can reach the ForestEndlessPathway3 by going "west".
it can reach the ForestEndlessPathway1 by going "east".
it can reach the ForestEndlessPathway2 by going "south".

a ForestEndlessPathway3 is a kind of place.
it can reach the ForestEndlessPathway1 by going "west".
it can reach the ForestEndlessPathway2 by going "south".
it can reach the ForestEntrance by going "east".
it can reach the ForestSpiritShrine by going "north".

a ForestSpiritShrine is a kind of place.
it can reach the ForestEntrance by going "north".
it can reach the ForestEndlessPathway1 by going "west".
it can reach the FairyGlen by going "east".
it can reach the ForestEndlessPathway3 by going "south".

a FairyGlen is a kind of place.
it contains 1 AmuletRelic, 1 AmuletPillar.
it can reach the CalmingBrook by going "north".
it can reach the ForestSpiritShrine by going "west".

a CalmingBrook is a kind of place.
it can reach the FairyGlen by going "south".
it can reach the BoatTransport by going "west".

a BoatTransport is a kind of place.
it can reach the CalmingBrook by going "east".
it can reach the GrassyHill by going "west".

###ITEMS###
a Relic is a kind of item.
it has a value called worthAmount that is 10.

a StatueRelic is a kind of Relic.
it is described as "It's a small jade figurine, beautifully cut and sparkles slightly in the light. It looks old and expensive.".
it is observed as "You see a small jade statue on the ground.".

an AmuletRelic is a kind of Relic.
it is described as "It's an amulet on a thin golden chain with a brilliant emerald embedded in the middle.".
it is observed as "You see what appears to be an amulet laying on a pedestal at the center of the clearing.".

a RelicPouch is a kind of item.
it is described as "It's a small leather pouch with a drawstring at the top. Nothing special.".
it is observed as "You see a small leather pouch on the ground.".
it contains 1 StatueRelic.
it is also known as a "pouch".

an Armor is a kind of item.
it is observed as "There is a piece of armor on the ground.".
it has a value called protectionAmount that is 1.
it is also known as an "armor".

a LeatherArmor is a kind of Armor.
it is observed as "There is a set of leather armor on the ground.".
it has a value called protectionAmount that is 3.
it is also known as an "armor".

a Weapon is a kind of item.
it has a value called damageAmount that is 1.

a Sword is a kind of Weapon.
it is observed as "There is a sword laying on the ground.".
it has a value called damageAmount that is 3.
it is also known as a "sword".

a Staff is a kind of Weapon.

a Dagger is a kind of Weapon.
it has a value called damageAmount that is 2.

a Coin is a kind of item. 
it is described as "It's a small gold coin, engraved with somebody's likeness.".
it can be grouped as CoinGroup.

a CoinGroup is a kind of group.
it is observed as "You see {{count}} coins!".
it is listed as "{{count}} gold coins".
it can contain any Coin.
it is also known as a "coins".
when it is described:
    say "It's a pile of gold coins";
and then stop.

###CREATURES###
a Monk is a kind of creature.
it is described as "The monk's ancient face is lined with wrinkles but still holds a youthful energy about him.".
it is observed in the SecludedTemple as "A monk with a bald head and plain yellowish orange robes is sweeping the entrance to the temple.".
it is observed in the SecludedTempleGarden as "A monk with a watering can and trowel is tending to the flower beds.".
it is also known as a "monk", "man".
when it is given a Coin in the SecludedTemple:
    say "He pauses sweeping and accepts the coin, placing it into a small donation box on the steps of the temple. He smiles and bows, then resumes his work.";
    move Coin to TempleDonationBox;
and then stop.
when it is given a Coin in the SecludedTempleGarden:
    say "He gladly accepts the coin with a kind smile, and then heads back into the temple with it, presumably to store it somewhere.";
    move Coin to TempleDonationBox;
    remove it from SecludedTempleGarden;
and then stop.

###DECORATIONS###
a TempleDonationBox is a kind of Container.
it is described as "The box is plain and made of sturdy wood. There's a slot on top that seems about the size of a coin. The paper attached to the front reads 'Please Give What You Can'.".
it is observed as "A plain wooden box sits on the porch with a paper attached to it.".
it is "locked".
it is also known as a "box".
when it is opened:
    if it is "locked" then
        say "The box appears to have a small lock that's keeping it shut.";
        abort event;
    and then continue;
and then stop.
when it is used with a Coin:
    say "The monk sees you put the coin in the box and smiles his thanks to you.";
    move Coin to it;
and then stop.

a TempleDonationBoxPaper is a kind of decoration.
it is described as "The paper is faded and yellowing from age. Somebody has written 'Please Give What You Can' on it.".
it is also known as a "paper".

a WaterloggedCorpse is a kind of decoration.
it is described as "The body seems to have taken on a lot of water over time and begun to decay. It has a disgusting stench.".
it is observed in the CliffBottom as "There appears to be the remains of a human body that's washed up on the beach, dressed in the remains of its clothing.".

an AmuletPillar is a kind of decoration.
it is described as "The pillar looks to be made from some kind of polished stone and engraved with depictions of spirits and fairies.".